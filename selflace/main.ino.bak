#include <PID_v1.h>

const int FSR_F_PIN = A0; // the actual force sensor
const int FSR_S_PIN = A1; // is there a shoe?
const int POT_PIN = A2;   // change force

const int SPEED_PIN = 3;    // hbridge enable
const int MOTOR_1A_PIN = 6; // hbridge leg 1
const int MOTOR_2A_PIN = 7; // hbridge leg 2

// PID stuff!
double Kp = 12, Ki = 0, Kd = 0;
double m_setpoint, m_input, m_output;
PID m_control{&m_input, &m_output, &m_setpoint, Kp, Ki, Kd, DIRECT};

void motorCtrl_fwd() { // set motor controller to forward motion
  digitalWrite(MOTOR_1A_PIN, LOW);
  digitalWrite(MOTOR_2A_PIN, HIGH);
}

void motorCtrl_rev() { // set motor controller to reverse motion
  digitalWrite(MOTOR_1A_PIN, HIGH);
  digitalWrite(MOTOR_2A_PIN, LOW);
}

void motorCtrl_write(int val) { // set motor controller to given power
  if (val > 0) {
    motorCtrl_fwd();
    analogWrite(SPEED_PIN, val);
  } else if (val < 0) {
    motorCtrl_rev();
    analogWrite(SPEED_PIN, 255-val);
  } else {
    motorCtrl_fwd();
    analogWrite(SPEED_PIN, 0);
  }
}

bool fsr_isDetected() { // is the bottom FSR detecting a human?
  return (analogRead(FSR_S_PIN) > 700); // threshold for mg
}

int fsr_getAnalog() { // returns the raw value of the force FSR
  return analogRead(FSR_F_PIN);
}

int fsr_getNewtons() { // "should" (RFC 2119) return the FSR value in Newtons
  int fsrVoltage = map(fsr_getAnalog(), 0, 1023, 0, 5000); // 0-1023 to 0-5V (5000 mV)
  long fsrForce;

  if (fsrVoltage == 0) {
    return 0; // no net force on the sensor
  } else {
    // the voltage = vcc * R / (R + FSR) where R = 10K and Vcc = 5V
    // so FSR = ((Vcc - V) * R) / V
    unsigned long fsrResistance = 5000 - fsrVoltage;
    fsrResistance *= 10000;
    fsrResistance /= fsrVoltage;

    unsigned long fsrConductance = 1000000; // microMhos
    fsrConductance /= fsrResistance;

    // fsr graphs from datasheet: https://cdn-shop.adafruit.com/datasheets/FSR400Series_PD.pdf
    if (fsrConductance <= 1000) {
      fsrForce = fsrConductance / 80;
    } else {
      fsrForce = fsrConductance - 1000;
      fsrForce /= 30;
    }
  }

  return fsrForce;
}

int pot_getAnalog() {
  return analogRead(POT_PIN);
}

int pot_getSetpoint() {
  return map(pot_getAnalog(), 0, 1023, 0, 50); // last value is threshold
}

void setup() {
  pinMode(SPEED_PIN, OUTPUT);
  pinMode(MOTOR_1A_PIN, OUTPUT);
  pinMode(MOTOR_2A_PIN, OUTPUT);

  m_input = fsr_getNewtons();
  m_setpoint = 15; // default
  
  Serial.begin(9600);
  m_control.SetMode(AUTOMATIC); // turn the PID on
}

void loop() {
  m_input = fsr_getNewtons();
  m_setpoint = pot_getSetpoint();

  int sgn = 0;
  if (m_input > m_setpoint) {
    m_control.SetControllerDirection(REVERSE);
    sgn = -1;
  } else {
    m_control.SetControllerDirection(DIRECT);
    sgn = 1;
  }

  m_control.Compute();
  m_output *= sgn;

  if (fsr_isDetected()) {
    motorCtrl_write(m_output);
  } else {
    motorCtrl_write(0);
  }

  Serial.print("FSR value (N): ");
  Serial.println(m_input);
  Serial.print("Attempted output: ");
  Serial.println(m_output);
  Serial.print("Pot value: ");
  Serial.println(pot_getSetpoint());
  delay(100); // i have no clue why this works. but it does. so leave me alone...
              // also it helps with serial debug... i guess...
}
